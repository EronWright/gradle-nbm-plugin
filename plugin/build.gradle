apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

def compatibilityVersion = 1.7
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

group='cz.kubacki.gradle.plugins'
version='1.0-SNAPSHOT'

// if (System.getProperty("test.netbeans.harness.dir") == null) {
if (!hasProperty("test.netbeans.harness.dir")) {
    throw new GradleException("NetBeans harness not found. Pass its location with -Ptest.netbeans.harness.dir=<harness_dir> or add to gradle.properties file.")
} else {
    println 'Using ' + property("test.netbeans.harness.dir")
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "org.gradle:gradle-tooling-api:${gradle.gradleVersion}"
  compile "org.codehaus.mojo:nbm-maven-harness:7.4"
  runtime 'org.slf4j:slf4j-simple:1.7.5'

  testCompile "org.spockframework:spock-core:0.7-groovy-1.8", {
    exclude module: "groovy-all"
  }
  testCompile "junit:junit:4.11"
}
repositories {
    mavenCentral()
    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }
}
 
sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integTest(type: Test, dependsOn: test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    reports.junitXml.destination = file("${project.testResultsDir}/$name")
    reports.html.destination = file("${project.reporting.baseDir}/$name")
}

check.dependsOn integTest

tasks.withType(Test) {
    systemProperty 'test.netbeans.harness.dir', project.property("test.netbeans.harness.dir")
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("$buildDir/repo"))
        }
    }
}
idea.module {
    testSourceDirs += file('src/integTest/groovy')
}
