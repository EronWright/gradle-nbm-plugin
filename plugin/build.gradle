buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
// apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

def compatibilityVersion = 1.7
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "org.gradle:gradle-tooling-api:${gradle.gradleVersion}"
  compile "org.codehaus.mojo:nbm-maven-harness:7.4"
  runtime 'org.slf4j:slf4j-simple:1.7.5'

  testCompile "org.spockframework:spock-core:0.7-groovy-1.8", {
    exclude module: "groovy-all"
  }
  testCompile "junit:junit:4.11"
  testCompile "org.hamcrest:hamcrest-library:1.3"
  testCompile "com.google.guava:guava:14.0.1"
}
repositories {
    mavenCentral()
    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }
}
 
sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integTest(type: Test, dependsOn: test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    reports.junitXml.destination = file("${project.testResultsDir}/$name")
    reports.html.destination = file("${project.reporting.baseDir}/$name")
}

check.dependsOn integTest

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'cz.kubacki.gradle.plugins'
            artifactId 'gradle-nbm-plugin'
            version project.hasProperty('BUILD_VERSION') ? project.BUILD_VERSION : '1.0-SNAPSHOT'

            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            url uri("$buildDir/repo")
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        // userOrg = 'nbandroid'
        name = 'gradle-nbm-plugin'
        licenses = ['Apache-2.0']
    }
}

idea.module {
    testSourceDirs += file('src/integTest/groovy')
}
